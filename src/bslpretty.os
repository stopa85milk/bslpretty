
#Использовать osparser

Функция Настройки()
	
	Настройки = Новый Структура();
	
	// необходим ли интерактивный режим работы?
	Настройки.Вставить("ИнтерактивныйРежимРаботы", Истина);
	
	// Для использования утилиты Diff нужно вызвать вот такую команду. Количество ковычек сводит с ума
	// > cmd /K ""C:\Program Files\Git\usr\bin\diff.exe" -C 3 --color=auto "c:\git\bslpretty\src\bslpretty.os" "C:\Users\Сергей Якушев\AppData\Local\Temp\d14i4tyz4bv.tmp""
	Настройки.Вставить("УтилитаСравнения", "cmd /K """"C:\Program Files\Git\usr\bin\diff.exe"" -C 3 --color=auto ""%1"" ""%2""""");
	
	// Чуть более наглядоное сравнение с помощью утилиты WinMerge
	Настройки.Вставить("УтилитаСравнения", """C:\Program Files (x86)\WinMerge\WinMergeU.exe"" ""%1"" ""%2""");
	
	Настройки.Вставить("ПараметрыОформителяОтступов", Новый Структура());
	Настройки.ПараметрыОформителяОтступов.Вставить("ПустыеСтрокиСОтступами",           Истина);
	Настройки.ПараметрыОформителяОтступов.Вставить("Отступ",                           Строка(Символы.Таб));
	Настройки.ПараметрыОформителяОтступов.Вставить("РекурсивныеДополнительныеОтступы", Ложь);
	
	Настройки.Вставить("ПараметрыВыравниванияАргументов", Новый Структура());
	Настройки.ПараметрыВыравниванияАргументов.Вставить("Использовать", Истина);
	Настройки.ПараметрыВыравниванияАргументов.Вставить("КоличествоСтрокПодряд", 3);
	
	Возврат Настройки;
	
КонецФункции

Функция СпроситьПользователяДаНет(ТекстВопроса)
	
	Перем ОтветТекст;
	
	ВариантыОтветов = Новый Структура();
	ВариантыОтветов.Вставить("Да", Истина);
	ВариантыОтветов.Вставить("Нет", Ложь);
	ВариантыОтветов.Вставить("Yes", Истина);
	ВариантыОтветов.Вставить("No", Ложь);
	ВариантыОтветов.Вставить("Д", Истина);
	ВариантыОтветов.Вставить("Н", Ложь);
	ВариантыОтветов.Вставить("Y", Истина);
	ВариантыОтветов.Вставить("N", Ложь);
	
	ОтветПользователя = Неопределено;
	Пока ОтветПользователя = Неопределено Цикл
		
		ВвестиСтроку(ОтветТекст, ТекстВопроса + "(Да/Нет)", 3);
		Если ВариантыОтветов.Свойство(ОтветТекст, ОтветПользователя) Тогда
			Прервать;
		Иначе
			Сообщить("Непонятный ответ");
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтветПользователя;
	
КонецФункции

Функция ИнформацияОМодификацииФайла()
	
	Результат = Новый Структура();
	Результат.Вставить("ИмяФайла", Неопределено);
	Результат.Вставить("ИзмененныеСтроки", Неопределено);
	
	Возврат Результат;
	
КонецФункции

Процедура ВывестиСправку()
	
	Сообщить("Использование
		|bslpretty.os git путь_к_репозиторию [commit]
		|  Оформляет измененные стройки по результату сравнения git diff [commit]
		|  Оформление применяется только к измененным строкам в файлах
		|
		|bslpretty.os file путь_к_каталогу_с_файлами
		| Оформляет все файлы *.bsl и *.os в каталоге");
	
	ЗавершитьРаботу(1);
	
КонецПроцедуры

Функция ПолучитьФайлыДляОформления()
	
	Если АргументыКоманднойСтроки.Количество() < 2 Тогда
		ВывестиСправку();
	КонецЕсли;
	
	Если НРег(АргументыКоманднойСтроки[0]) = "git" Тогда
		
		Коммит = "";
		Если АргументыКоманднойСтроки.Количество() = 3 Тогда
			
			Коммит = АргументыКоманднойСтроки[1];
			
		КонецЕсли;
		
		Возврат ПолучитьМодифицированныеФайлыGIT(АргументыКоманднойСтроки[1], Коммит);
		
	ИначеЕсли НРег(АргументыКоманднойСтроки[0]) = "file" Тогда
		
		Возврат НайтиФайлыРекурсивно(АргументыКоманднойСтроки[1]);
		
	Иначе
		
		ВывестиСправку();
		
	КонецЕсли;
	
	Возврат Новый Массив();
	
КонецФункции

Функция НайтиФайлыРекурсивно(ПутьКФайлам)
	
	Файлы = НайтиФайлы(ПутьКФайлам, "*", Истина);
	Результат = Новый Массив();
	Для Каждого Файл Из Файлы Цикл
		Инф = ИнформацияОМодификацииФайла();
		Инф.ИмяФайла = Файл.ПолноеИмя;
		Результат.Добавить(Инф);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьМодифицированныеФайлыGIT(КаталогРепозитория, Коммит = "")
	
	Результат = Новый Массив();
	
	//Измененные файлы
	ЛогГит = ПолучитьИмяВременногоФайла();
	КодВозврата = -1;
	ЗапуститьПриложение(СтрШаблон("cmd /C git %1 diff -U0 > ""%2""", Коммит, ЛогГит), КаталогРепозитория, Истина, КодВозврата);
	Если КодВозврата <> 0 Тогда
		
		Сообщить("Ошибка получения git diff");
		Возврат Неопределено;
		
	КонецЕсли;
	
	ЧТ = Новый ЧтениеТекста(ЛогГит, КодировкаТекста.UTF8);
	
	Стр = ЧТ.ПрочитатьСтроку();
	
	ТекИнф = ИнформацияОМодификацииФайла();
	
	Пока Стр <> Неопределено Цикл
		
		Если Лев(Стр, 6) = "+++ b/" Тогда
			
			Если ТекИнф.ИмяФайла <> Неопределено Тогда
				Результат.Добавить(ТекИнф);
				ТекИнф = ИнформацияОМодификацииФайла();
			КонецЕсли;
			
			ТекИнф.ИмяФайла = КаталогРепозитория + "/" + Прав(Стр, СтрДлина(Стр) - 6);
			
		ИначеЕсли Лев(Стр, 2) = "@@" Тогда
			
			ИдентификаторИзменений = СтрРазделить(Стр, " ")[2];
			
			ИндексНовойСтроки = Неопределено;
			КоличествоНовыхСтрок = 1;
			Если Лев(ИдентификаторИзменений, 1) = "+" Тогда
				
				Подстроки = СтрРазделить(ИдентификаторИзменений, ",");
				ИндексНовойСтроки = Число(Прав(Подстроки[0], СтрДлина(Подстроки[0]) - 1));
				
				Если Подстроки.Количество() > 1 Тогда
					КоличествоНовыхСтрок = Число(Подстроки[1]);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ИндексНовойСтроки <> Неопределено Тогда
				
				Если ТекИнф.ИзмененныеСтроки = Неопределено Тогда
					ТекИнф.ИзмененныеСтроки = Новый Массив();
				КонецЕсли;
				
				Для ц = 0 По КоличествоНовыхСтрок - 1 Цикл
					ТекИнф.ИзмененныеСтроки.Добавить(ИндексНовойСтроки + ц);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Стр = ЧТ.ПрочитатьСтроку();
		
	КонецЦикла;
	
	Если ТекИнф.ИмяФайла <> Неопределено Тогда
		Результат.Добавить(ТекИнф);
		ТекИнф = ИнформацияОМодификацииФайла();
	КонецЕсли;
	
	//Новые файлы
	ЛогГит = ПолучитьИмяВременногоФайла();
	КодВозврата = -1;
	ЗапуститьПриложение("cmd /C git status -s > """ + ЛогГит + """", КаталогРепозитория, Истина, КодВозврата);
	Если КодВозврата <> 0 Тогда
		
		Сообщить("Ошибка получения git status");
		Возврат Неопределено;
		
	КонецЕсли;
	
	ЧТ = Новый ЧтениеТекста(ЛогГит, КодировкаТекста.UTF8);
	
	Стр = ЧТ.ПрочитатьСтроку();
	Пока Стр <> Неопределено Цикл
		
		Если Лев(Стр, 2) = "??" Тогда
			
			ЭлРезультата = ИнформацияОМодификацииФайла();
			ЭлРезультата.ИмяФайла = Прав(Стр, СтрДлина(Стр) - 3);
			
			Результат.Добавить(ЭлРезультата);
			
		КонецЕсли;
		
		Стр = ЧТ.ПрочитатьСтроку();
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

МодифицированныеФайлы = ПолучитьФайлыДляОформления();

ПодключитьСценарий("src\ОформительОтступов\Ext\ObjectModule.bsl", "ОформительОтступов");
ПодключитьСценарий("src\ОформительПробелов\Ext\ObjectModule.bsl", "ОформительПробелов");
ПодключитьСценарий("src\ЗаменаНеканоничныхКлючевыхСлов.os", "ЗаменаНеканоничныхКлючевыхСлов");
ПодключитьСценарий("src\ВыравниваниеАргументов\Ext\ObjectModule.bsl", "ВыравниваниеАргументов");
Настройки = Настройки();

Парсер = Новый ПарсерВстроенногоЯзыка;

ПлагинОформительОтступов = Новый ОформительОтступов;
ПлагинВыравниваниеАргументов = Новый ВыравниваниеАргументов;

Плагины = Новый Массив;
Плагины.Добавить(ПлагинОформительОтступов);
Плагины.Добавить(Новый ОформительПробелов);
Плагины.Добавить(Новый ЗаменаНеканоничныхКлючевыхСлов);

ПараметрыПлагинов = Новый Соответствие;
ПараметрыПлагинов[ПлагинОформительОтступов] = Настройки.ПараметрыОформителяОтступов;
ПараметрыПлагинов[ПлагинВыравниваниеАргументов] = Настройки.ПараметрыВыравниванияАргументов;

Для Каждого Инф Из МодифицированныеФайлы Цикл
	
	Если НРег(Прав(Инф.ИмяФайла, 4)) <> ".bsl"
		И НРег(Прав(Инф.ИмяФайла, 3)) <> ".os" Тогда
		
		Продолжить;
		
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(Инф.ИмяФайла, "UTF-8");
	Исходник = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	
	Попытка
		
		БылиИсправления = Ложь;
		
		Парсер.Пуск(Исходник, Плагины, ПараметрыПлагинов);
		ИсправленныйИсходник = Парсер.ВыполнитьЗамены();
		Если ИсправленныйИсходник = Неопределено Тогда
			
			ИсправленныйИсходник = Исходник;
			
		Иначе
			
			БылиИсправления = Истина;
			
		КонецЕсли;
		
		Если Настройки.ПараметрыВыравниванияАргументов.Использовать Тогда
			
			// Использование данного плагина требует заново распарсить код
			Исходник = ИсправленныйИсходник;
			
			Плагины = Новый Массив();
			Плагины.Добавить(ПлагинВыравниваниеАргументов);
			
			Парсер.Пуск(Исходник, Плагины, ПараметрыПлагинов);
			ИсправленныйИсходник = Парсер.ВыполнитьЗамены();
			Если ИсправленныйИсходник = Неопределено Тогда
				
				ИсправленныйИсходник = Исходник;
				
			Иначе
				
				БылиИсправления = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не БылиИсправления Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаписьТекста = Новый ЗаписьТекста;
		ЗаписьТекста.Открыть(ИмяВременногоФайла, "UTF-8");
		
		Если Инф.ИзмененныеСтроки = Неопределено Тогда
			
			ЗаписьТекста.Записать(ИсправленныйИсходник);
			
		Иначе
			
			МассивИсходник = СтрРазделить(Исходник, Строка(Символы.ПС), Истина);
			МассивИсправленный = СтрРазделить(ИсправленныйИсходник, Строка(Символы.ПС), Истина);
			
			Для Ц = 0 По МассивИсходник.Количество() - 1 Цикл
				
				Если Инф.ИзмененныеСтроки.Найти(ц + 1) = Неопределено Тогда
					ЗаписьТекста.ЗаписатьСтроку(МассивИсходник[ц]);
				Иначе
					ЗаписьТекста.ЗаписатьСтроку(МассивИсправленный[ц]);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЗаписьТекста.Закрыть();
		
	Исключение
		
		Сообщить(СтрШаблон("Ошибка парсинга файла %1", Инф.ИмяФайла));
		Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Продолжить;
		
	КонецПопытки;
	
	ПерезаписатьФайл = Неопределено;
	Если Не Настройки.ИнтерактивныйРежимРаботы Тогда
		
		ПерезаписатьФайл = Истина;
		
	Иначе
		
		КомандаDiff = СтрШаблон(Настройки.УтилитаСравнения, Инф.ИмяФайла, ИмяВременногоФайла);
		ЗапуститьПриложение(КомандаDiff,, Истина);
		
		ТекстВопроса = "Применить изменения к файлу " + Инф.ИмяФайла + "?";
		ПерезаписатьФайл = СпроситьПользователяДаНет(ТекстВопроса);
		
	КонецЕсли;
	
	Если ПерезаписатьФайл Тогда
		
		ПереместитьФайл(Инф.ИмяФайла, Инф.ИмяФайла + ".tmp");
		ПереместитьФайл(ИмяВременногоФайла, Инф.ИмяФайла);
		УдалитьФайлы(Инф.ИмяФайла + ".tmp");
		
	КонецЕсли;
	
КонецЦикла;
