
//пока не знаю как выбрать каталог
Перем КаталогРепозитория;

Функция ИнформацияОбМодификацииФайла()

	Результат = новый Структура();
	Результат.Вставить("ИмяФайла", Неопределено);
	Результат.Вставить("ИзмененныеСтроки", Неопределено);
	
	Возврат Результат;
	
КонецФункции

Процедура ПечатьИзмененийВФайле(ИнформацияОбМодификацииФайла)
	
	Сообщить("Имя файла " + ИнформацияОбМодификацииФайла.ИмяФайла);
	Если ИнформацияОбМодификацииФайла.ИзмененныеСтроки = Неопределено Тогда
		Сообщить("Весь файл");
	Иначе
		Сообщить("Строки " + СтрСоединить(ИнформацияОбМодификацииФайла.ИзмененныеСтроки, ", "));
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьМодифицированныеФайлы()

	Результат = новый Массив();
	
	//Измененные файлы
	ЛогГит = ПолучитьИмяВременногоФайла();
	КодВозврата = -1;
	ЗапуститьПриложение("cmd /C git diff -U0 > """ + ЛогГит + """", КаталогРепозитория, Истина, КодВозврата);
	Если КодВозврата <> 0 Тогда
		
		Сообщить("Ошибка получения git diff");
		Возврат Неопределено;
		
	КонецЕсли;

	ЧТ = новый ЧтениеТекста(ЛогГит, КодировкаТекста.UTF8);
	
	Стр = ЧТ.ПрочитатьСтроку();
	
	ТекИнф = ИнформацияОбМодификацииФайла();
	
	Пока Стр <> Неопределено Цикл
		
		Если Лев(Стр, 6) = "+++ b/" Тогда
			
			Если ТекИнф.ИмяФайла <> Неопределено Тогда
				Результат.Добавить(ТекИнф);
				ТекИнф = ИнформацияОбМодификацииФайла();
			КонецЕсли;
			
			ТекИнф.ИмяФайла = Прав(Стр, СтрДлина(Стр)-6);
		
		ИначеЕсли Лев(Стр, 2) = "@@" Тогда
			
			ИдентификаторИзменений = СтрРазделить(Стр, " ")[2];
			
			ИндексНовойСтроки = Неопределено;
			КоличествоНовыхСтрок = 1;
			Если Лев(ИдентификаторИзменений, 1) = "+" Тогда
				
				Подстроки = СтрРазделить(ИдентификаторИзменений, ",");
				ИндексНовойСтроки = Число(Прав(Подстроки[0], СтрДлина(Подстроки[0])-1));
				
				Если Подстроки.Количество()>1 Тогда
					КоличествоНовыхСтрок = Число(Подстроки[1]);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ИндексНовойСтроки <> Неопределено Тогда
				
				Если ТекИнф.ИзмененныеСтроки = Неопределено Тогда
					ТекИнф.ИзмененныеСтроки = новый Массив();
				КонецЕсли;
				
				Для ц = 0 По КоличествоНовыхСтрок - 1 Цикл
					ТекИнф.ИзмененныеСтроки.Добавить(ИндексНовойСтроки + ц);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Стр = ЧТ.ПрочитатьСтроку();
		
	КонецЦикла;
	
	Если ТекИнф.ИмяФайла <> Неопределено Тогда
		Результат.Добавить(ТекИнф);
		ТекИнф = ИнформацияОбМодификацииФайла();
	КонецЕсли;

	//Новые файлы
	ЛогГит = ПолучитьИмяВременногоФайла();
	КодВозврата = -1;
	ЗапуститьПриложение("cmd /C git status -s > """ + ЛогГит + """", КаталогРепозитория, Истина, КодВозврата);
	Если КодВозврата <> 0 Тогда
		
		Сообщить("Ошибка получения git status");
		Возврат Неопределено;
		
	КонецЕсли;

	ЧТ = новый ЧтениеТекста(ЛогГит, КодировкаТекста.UTF8);
	
	Стр = ЧТ.ПрочитатьСтроку();
	Пока Стр <> Неопределено Цикл
		
		Если Лев(Стр, 2) = "??" Тогда
			
			ЭлРезультата = ИнформацияОбМодификацииФайла();
			ЭлРезультата.ИмяФайла = Прав(Стр, СтрДлина(Стр)-3);
			
			Результат.Добавить(ЭлРезультата);
			
		КонецЕсли;
		
		Стр = ЧТ.ПрочитатьСтроку();
		
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

КаталогРепозитория = "C:\git\bslpretty\";
МодифицированныеФайлы = ПолучитьМодифицированныеФайлы();

Для каждого Инф ИЗ МодифицированныеФайлы Цикл
	ПечатьИзмененийВФайле(Инф);
КонецЦикла;
